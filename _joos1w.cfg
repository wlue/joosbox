Goal:

  CompilationUnit

Literal:

  IntegerLiteral

  BooleanLiteral

  CharLiteral

  StringLiteral

  NullLiteral

IntegerLiteral:

  Num

BooleanLiteral:

  TrueLiteral

  FalseLiteral

Type:

  PrimitiveType

  ReferenceType

PrimitiveType:

  NumericType

  BooleanKeyword

NumericType:

  ByteKeyword

  ShortKeyword

  IntKeyword

  CharKeyword

ReferenceType:

  ClassOrInterfaceType

  ArrayType

ClassOrInterfaceType:

  Name

ClassType:

  ClassOrInterfaceType

InterfaceType:

  ClassOrInterfaceType

ArrayType:

  PrimitiveType LeftBracket RightBracket

Name:

  SimpleName

  QualifiedName

SimpleName:

  Identifier

QualifiedName:

  Name Dot Identifier

CompilationUnit:

  PackageDeclaration ImportDeclarations TypeDeclarations

  PackageDeclaration ImportDeclarations

  PackageDeclaration

  ImportDeclarations TypeDeclarations

  ImportDeclarations

  TypeDeclarations

ImportDeclarations:

  ImportDeclaration

  ImportDeclarations ImportDeclaration

TypeDeclarations:

  TypeDeclaration

  TypeDeclarations TypeDeclaration

PackageDeclaration:

  PackageKeyword Name Semicolon

ImportDeclaration:

  SingleTypeImportDeclaration

  TypeImportOnDemandDeclaration

SingleTypeImportDeclaration:

  ImportKeyword Name Semicolon

TypeImportOnDemandDeclaration:

  ImportKeyword Name Dot Star Semicolon

TypeDeclaration:

  ClassDeclaration

  InterfaceDeclaration

  Semicolon

Modifiers:

  Modifier

  Modifiers Modifier

Modifier:

  AccessModifier

  StaticModifier

  NonAccessModifier

AccessModifier:

  PublicKeyword

  ProtectedKeyword

  PrivateKeyword

StaticModifier:

  StaticKeyword

NonAccessModifier:

  AbstractKeyword

  FinalKeyword

  NativeKeyword

ClassDeclaration:

  Modifiers ClassKeyword Identifier Super Interfaces ClassBody

  Modifiers ClassKeyword Identifier Interfaces ClassBody

  Modifiers ClassKeyword Identifier Super ClassBody

  Modifiers ClassKeyword Identifier ClassBody

  ClassKeyword Identifier Super Interfaces ClassBody

  ClassKeyword Identifier Interfaces ClassBody

  ClassKeyword Identifier Super ClassBody

  ClassKeyword Identifier ClassBody

Super:

  ExtendsKeyword ClassType

Interfaces:

  ImplementsKeyword InterfaceTypeList

InterfaceTypeList:

  InterfaceType

  InterfaceTypeList Comma InterfaceType

ClassBody:

  LeftCurly ClassBodyDeclarations RightCurly

  LeftCurly RightCurly

ClassBodyDeclarations:

  ClassBodyDeclaration

  ClassBodyDeclarations ClassBodyDeclaration

ClassBodyDeclaration:

  ClassMemberDeclaration

  StaticInitializer

  ConstructorDeclaration

ClassMemberDeclaration:

  FieldDeclaration

  MethodDeclaration

FieldDeclaration:

  Modifiers Type VariableDeclarators Semicolon

  Type VariableDeclarators Semicolon

VariableDeclarators:

  VariableDeclarator

VariableDeclarator:

  VariableDeclaratorId

  VariableDeclaratorId Assign Expression

VariableDeclaratorId:

  Identifier

  VariableDeclaratorId LeftBracket RightBracket

MethodDeclaration:

  MethodHeader MethodBody

MethodHeader:

  Modifiers Type MethodDeclarator

  Type MethodDeclarator

  Modifiers VoidKeyword MethodDeclarator

  VoidKeyword MethodDeclarator

MethodDeclarator:

  Identifier LeftParen FormalParameterList RightParen

  Identifier LeftParen RightParen

FormalParameterList:

  FormalParameter

  FormalParameterList Comma FormalParameter

FormalParameter:

  Type VariableDeclaratorId

MethodBody:

  Block

  Semicolon

StaticInitializer:

  StaticKeyword Block

ConstructorDeclaration:

  Modifiers ConstructorDeclarator ConstructorBody

  ConstructorDeclarator ConstructorBody

ConstructorDeclarator:

  SimpleName LeftParen FormalParameterList RightParen

  SimpleName LeftParen RightParen

ConstructorBody:

  LeftCurly ExplicitConstructorInvocation BlockStatements RightCurly

  LeftCurly BlockStatements RightCurly

  LeftCurly ExplicitConstructorInvocation RightCurly

  LeftCurly RightCurly

ExplicitConstructorInvocation:

  ThisKeyword LeftParen ArgumentList RightParen Semicolon

  ThisKeyword LeftParen RightParen Semicolon

  SuperKeyword LeftParen ArgumentList RightParen Semicolon

  SuperKeyword LeftParen RightParen Semicolon

InterfaceDeclaration:

  Modifiers InterfaceKeyword Identifier ExtendsInterfaces InterfaceBody

  InterfaceKeyword Identifier ExtendsInterfaces InterfaceBody

  Modifiers InterfaceKeyword Identifier InterfaceBody

  InterfaceKeyword Identifier InterfaceBody

ExtendsInterfaces:

  ExtendsKeyword InterfaceType

  ExtendsInterfaces Comma InterfaceType

InterfaceBody:

  LeftCurly InterfaceMemberDeclarations RightCurly

  LeftCurly RightCurly

InterfaceMemberDeclarations:

  InterfaceMemberDeclaration

  InterfaceMemberDeclarations InterfaceMemberDeclaration

InterfaceMemberDeclaration:

  ConstantDeclaration

  AbstractMethodDeclaration

ConstantDeclaration:

  FieldDeclaration

AbstractMethodDeclaration:

  MethodHeader Semicolon

Block:

  LeftCurly BlockStatements RightCurly

  LeftCurly RightCurly

BlockStatements:

  BlockStatement

  BlockStatements BlockStatement

BlockStatement:

  LocalVariableDeclarationStatement

  Statement

LocalVariableDeclarationStatement:

  LocalVariableDeclaration Semicolon

LocalVariableDeclaration:

  Type VariableDeclarators

Statement:

  StatementWithoutTrailingSubstatement

  IfThenStatement

  IfThenElseStatement

  WhileStatement

  ForStatement

StatementNoShortIf:

  StatementWithoutTrailingSubstatement

  IfThenElseStatementNoShortIf

  WhileStatementNoShortIf

  ForStatementNoShortIf

StatementWithoutTrailingSubstatement:

  Block

  EmptyStatement

  ExpressionStatement

  ReturnStatement

EmptyStatement:

  Semicolon

ExpressionStatement:

  StatementExpression Semicolon

StatementExpression:

  Assignment

  MethodInvocation

  ClassInstanceCreationExpression

IfThenStatement:

  IfKeyword LeftParen Expression RightParen Statement

IfThenElseStatement:

  IfKeyword LeftParen Expression RightParen StatementNoShortIf ElseKeyword Statement

IfThenElseStatementNoShortIf:

  IfKeyword LeftParen Expression RightParen StatementNoShortIf ElseKeyword StatementNoShortIf

WhileStatement:

  WhileKeyword LeftParen Expression RightParen Statement

WhileStatementNoShortIf:

  WhileKeyword LeftParen Expression RightParen StatementNoShortIf

ForStatement:

  ForKeyword LeftParen ForInit Semicolon Expression Semicolon ForUpdate RightParen Statement

  ForKeyword LeftParen Semicolon Expression Semicolon ForUpdate RightParen Statement

  ForKeyword LeftParen ForInit Semicolon Semicolon ForUpdate RightParen Statement

  ForKeyword LeftParen Semicolon Semicolon ForUpdate RightParen Statement

  ForKeyword LeftParen ForInit Semicolon Expression Semicolon RightParen Statement

  ForKeyword LeftParen Semicolon Expression Semicolon RightParen Statement

  ForKeyword LeftParen ForInit Semicolon Semicolon RightParen Statement

  ForKeyword LeftParen Semicolon Semicolon RightParen Statement

ForStatementNoShortIf:

  ForKeyword LeftParen ForInit Semicolon Expression Semicolon ForUpdate RightParen StatementNoShortIf

  ForKeyword LeftParen Semicolon Expression Semicolon ForUpdate RightParen StatementNoShortIf

  ForKeyword LeftParen ForInit Semicolon Semicolon ForUpdate RightParen StatementNoShortIf

  ForKeyword LeftParen Semicolon Semicolon ForUpdate RightParen StatementNoShortIf

  ForKeyword LeftParen ForInit Semicolon Expression Semicolon RightParen StatementNoShortIf

  ForKeyword LeftParen Semicolon Expression Semicolon RightParen StatementNoShortIf

  ForKeyword LeftParen ForInit Semicolon Semicolon RightParen StatementNoShortIf

  ForKeyword LeftParen Semicolon Semicolon RightParen StatementNoShortIf

ForInit:

  StatementExpression

  Type VariableDeclarator

ForUpdate:

  StatementExpression

ReturnStatement:

  ReturnKeyword Expression Semicolon

  ReturnKeyword Semicolon

Primary:

  PrimaryNoNewArray

  ArrayCreationExpression

PrimaryNoNewArray:

  Literal

  ThisKeyword

  LeftParen Expression RightParen

  ClassInstanceCreationExpression

  FieldAccess

  MethodInvocation

  ArrayAccess

ClassInstanceCreationExpression:

  NewKeyword ClassType LeftParen ArgumentList RightParen

  NewKeyword ClassType LeftParen RightParen

ArgumentList:

  Expression

  ArgumentList Comma Expression

ArrayCreationExpression:

  NewKeyword PrimitiveType DimExprs Dims

  NewKeyword PrimitiveType DimExprs

  NewKeyword ClassOrInterfaceType DimExprs Dims

  NewKeyword ClassOrInterfaceType DimExprs

DimExprs:

  DimExpr

  DimExprs DimExpr

DimExpr:

  LeftBracket Expression RightBracket

Dims:

  LeftBracket RightBracket

  Dims LeftBracket RightBracket

FieldAccess:

  Primary Dot Identifier

  SuperKeyword Dot Identifier

MethodInvocation:

  Name LeftParen ArgumentList RightParen

  Name LeftParen RightParen

  Primary Dot Identifier LeftParen ArgumentList RightParen

  Primary Dot Identifier LeftParen RightParen

  SuperKeyword Dot Identifier LeftParen ArgumentList RightParen

  SuperKeyword Dot Identifier LeftParen RightParen

ArrayAccess:

  Name LeftBracket Expression RightBracket

  PrimaryNoNewArray LeftBracket Expression RightBracket

PostfixExpression:

  Primary

  Name

UnaryExpression:

  Plus UnaryExpression

  Minus UnaryExpression

  UnaryExpressionNotPlusMinus

UnaryExpressionNotPlusMinus:

  PostfixExpression

  BinaryNot UnaryExpression

  LogicalNot UnaryExpression

  CastExpression

CastExpression:

  LeftParen PrimitiveType Dims RightParen UnaryExpression

  LeftParen PrimitiveType RightParen UnaryExpression

  LeftParen Expression RightParen UnaryExpressionNotPlusMinus

  LeftParen Name Dims RightParen UnaryExpressionNotPlusMinus

MultiplicativeExpression:

  UnaryExpression

  MultiplicativeExpression Star UnaryExpression

  MultiplicativeExpression Divide UnaryExpression

  MultiplicativeExpression Modulo UnaryExpression

AdditiveExpression:

  MultiplicativeExpression

  AdditiveExpression Plus MultiplicativeExpression

  AdditiveExpression Minus MultiplicativeExpression

RelationalExpression:

  AdditiveExpression

  RelationalExpression LessThan AdditiveExpression

  RelationalExpression GreaterThan AdditiveExpression

  RelationalExpression LessEqual AdditiveExpression

  RelationalExpression GreaterEqual AdditiveExpression

  RelationalExpression InstanceofKeyword ReferenceType

EqualityExpression:

  RelationalExpression

  EqualityExpression Equal RelationalExpression

  EqualityExpression NotEqual RelationalExpression

AndExpression:

  EqualityExpression

  AndExpression BinaryAnd EqualityExpression

ExclusiveOrExpression:

  AndExpression

  ExclusiveOrExpression BinaryXor AndExpression

InclusiveOrExpression:

  ExclusiveOrExpression

  InclusiveOrExpression BinaryOr ExclusiveOrExpression

ConditionalAndExpression:

  InclusiveOrExpression

  ConditionalAndExpression LogicalAnd InclusiveOrExpression

ConditionalOrExpression:

  ConditionalAndExpression

  ConditionalOrExpression LogicalOr ConditionalAndExpression

ConditionalExpression:

  ConditionalOrExpression

AssignmentExpression:

  ConditionalExpression

  Assignment

Assignment:

  LeftHandSide Assign AssignmentExpression

LeftHandSide:

  Name

  FieldAccess

  ArrayAccess

Expression:

  AssignmentExpression

